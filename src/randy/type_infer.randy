// handle a minimal amount of errors here, defer most type errors to the type checker.

const TTYPE_INFER_cur_procs   = 0;  // stack of procs
const TTYPE_INFER_did_returns = 8;  // stack of bools
const TTYPE_INFER_again       = 16; // set of proc symbols
const sizeof_TTYPE_INFER = 24;

proc make_type_infer -> type_infer in
    var self = malloc(sizeof_TTYPE_INFER);
    u64!(self + TTYPE_INFER_cur_procs, make_vector(0));
    u64!(self + TTYPE_INFER_did_returns, make_string(0));
    u64!(self + TTYPE_INFER_again, make_set(0, NULL, NULL));
    return cast(self, type_infer);
end

proc type_infer_cur_procs self: type_infer in
    return cast(u64@(self + TTYPE_INFER_cur_procs), Vector&);
end
proc type_infer_did_returns self: type_infer in
    return cast(u64@(self + TTYPE_INFER_did_returns), String&);
end
proc type_infer_again self: type_infer in
    return cast(u64@(self + TTYPE_INFER_again), set);
end
proc type_infer_set_again self: type_infer, set: set in
    u64!(self + TTYPE_INFER_again, set);
end

proc free_type_infer self: type_infer in
    if self then
        free_vector(type_infer_cur_procs(self));
        free_string(type_infer_did_returns(self));
        free_set(type_infer_again(self));
        free(self);
    end
end

proc ti_current_proc self in
    return vector_back(type_infer_cur_procs(self));
end

proc ti_did_return self in
    return string_back(type_infer_did_returns(self));
end

proc ti_set_did_return self, val in
    var str = type_infer_did_returns(self);
    var len = str.length;
    if len > 0 then
        string_set(str, len - 1, val);
    end
end

proc ti_push_proc self, proc_symbol in
    vector_push(type_infer_cur_procs(self), proc_symbol);
    string_push(type_infer_did_returns(self), 0);
end

proc ti_pop_proc self in
    vector_pop(type_infer_cur_procs(self));
    string_pop(type_infer_did_returns(self));
end

proc ti_infer_body self, tt, st, body in
    var i = 0; var len = body.length;
    while i < len do
        var ast = vector_get(body, i);
        ti_infer(self, tt, st, ast);
        if ast_kind(ast) == AstK_Return then
            break;
        end
        i += 1;
    end
end

proc ti_infer_ident self, tt, st, ast -> type in
    check_ast_kind(AstK_Ident, ast);
    var ident = ast_ident(ast);
    var symbol = st_get_symbol(st, ident);
    var type = symbol_type(symbol);
    if type then
        return type;
    else
        symbol_set_type(symbol, tt_any_type(tt));
    end
    return tt_any_type(tt);
end

proc ti_infer_integer self, tt, st, ast -> type in
    check_ast_kind(AstK_Integer, ast);
    return tt_int_type(tt);
    // var val = cast(ast_value(ast), int);
    // var type;
    // if val >= -(1<<7) and val < 1<<7 then
    //     type = tt_get_type_or_null(tt, "int8");
    // elif val >= -(1<<15) and val < 1<<15 then
    //     type = tt_get_type_or_null(tt, "int16");
    // elif val >= -(1<<31) and val < 1<<31 then
    //     type = tt_get_type_or_null(tt, "int32");
    // else
    //     type = tt_int_type(tt);
    // end
    // assert(type);
    // return type;
end

proc ti_infer_string self, tt, st, ast -> type in
    check_ast_kind(AstK_String, ast);
    return tt_cstr_type(tt);
end

proc ti_infer_call self, tt, st, ast -> type in
    check_ast_kind(AstK_Call, ast);
    var expr = ast_expr(ast);
    var call_type = ti_infer(self, tt, st, expr);
    var any = tt_any_type(tt);
    // TODO: put this check in type checker
    if not type_is_procedure(call_type) then
        // FIXME: call_type should not accept ptr when type system is more fleshed out.
        if call_type != any and call_type != tt_ptr_type(tt) then
            printf("WARN: type `");
            tt_print_type_name(tt, call_type);
            printf("` is not a procedure.\n");
            printf("NOTE: The expression is: ");
            print_ast(ast);
            print_token_loc(ast_token(ast));
        end
        return tt_any_type(tt);
    end
    if ast_kind(expr) != AstK_Ident then
        return type_proc_return_type(call_type);
    end
    var call_symbol = st_get_symbol(st, ast_ident(expr));
    // if not symbol_is_proc(call_symbol) and
    //     not symbol_is_asm(call_symbol) and
    //     not symbol_is_extern(call_symbol) then
    //     return type_proc_return_type(call_type);
    // end
    call_type = symbol_type(call_symbol);
    assert(type_is_procedure(call_type), "call_type not procedure");
    var proc_ast = type_ast(call_type);
    var proc_params = NULL;
    if proc_ast then
        proc_params = ast_params(proc_ast);
    end

    // TODO: need to handle varargs at some point
    var param_types = type_proc_parameters(call_type);
    var args = ast_args(ast);
    var i = 0; var len = min(param_types.length, args.length);
    var changed = false;
    while i < len do
        var arg_type = ti_infer(self, tt, st, vector_get(args, i));
        assert(arg_type);
        var par_type = vector_get(param_types, i);
        assert(par_type);
        if arg_type != any and par_type == any then
            vector_set(param_types, i, arg_type);
            if proc_params then
                var par_sym = st_get_symbol(st, vector_get(proc_params, i));
                symbol_set_type(par_sym, arg_type);
            end
            changed = true;
        end
        i += 1;
    end
    len = args.length;
    while i < len do
        ti_infer(self, tt, st, vector_get(args, i));
        i += 1;
    end
    if changed then
        var proc_ast = type_ast(call_type);
        if proc_ast and not set_contains(type_infer_again(self), call_symbol) then
            set_add(type_infer_again(self), call_symbol);
            // FIXME: is there a better way to do this without recursion?
            ti_infer_procedure(self, tt, st, proc_ast);
        end
    end
    return type_proc_return_type(call_type);
end

proc ti_infer_binop self, tt, st, ast -> type in
    check_ast_kind(AstK_BinOp, ast);
    var lhs = ti_infer(self, tt, st, ast_lhs(ast));
    var rhs = ti_infer(self, tt, st, ast_rhs(ast));
    var op = ast_op(ast);
    if op == TK_Less or op == TK_LessEq or op == TK_Greater or op == TK_GreaterEq or
        op == TK_EqEq or op == TK_NotEq or op == TK_KW_and or op == TK_KW_or then
        return tt_bool_type(tt);
    end

    var any = tt_any_type(tt);
    var int = tt_int_type(tt);
    var cstr = tt_cstr_type(tt);
    var ptr = tt_ptr_type(tt);
    var char = tt_char_type(tt);

    if lhs == rhs then
        // pointer - pointer => int.
        if lhs == ptr and op == TK_Minus then
            return int;
        end
        return lhs;
    end

    if lhs == any and rhs == any then
        tt_set_ast_type(tt, ast_lhs(ast), int);
        tt_set_ast_type(tt, ast_rhs(ast), int);
        return int;
    end

    if lhs == any and rhs == int then
        tt_set_ast_type(tt, ast_lhs(ast), int);
        return int;
    end

    if lhs == cstr and rhs == int then
        return cstr;
    end

    if lhs == int and rhs == cstr then
        return cstr;
    end

    if lhs == int and rhs == any then
        tt_set_ast_type(tt, ast_rhs(ast), int);
        return int;
    end

    if lhs == ptr and rhs == int then
        return ptr;
    end

    if lhs == int and rhs == ptr then
        return ptr;
    end

    if type_is_int_like(lhs) and type_is_int_like(rhs) then
        if type_size(lhs) > type_size(rhs) then
            return lhs;
        else
            return rhs;
        end
    end
        
    return tt_any_type(tt);
end

proc ti_infer_return self, tt, st, ast -> type in
    check_ast_kind(AstK_Return, ast);
    var type;
    if ast_expr(ast) then
        type = ti_infer(self, tt, st, ast_expr(ast));
    else
        type = tt_void_type(tt);
    end
    var cur_proc = ti_current_proc(self);
    var proc_type = symbol_type(cur_proc);
    assert(type_is_procedure(proc_type), "proc_type not procedure type");
    var ret_type = type_proc_return_type(proc_type);
    var any = tt_any_type(tt);
    if not ret_type or ret_type == any then
        type_set_proc_return_type(proc_type, type);
    elif type_is_reference(ret_type) and type == any then
        // nothing
    elif not types_assignable(tt, type, ret_type) then
        // error message is handled in type checker
        return tt_void_type(tt);
    end
    ti_set_did_return(self, true);
    return tt_void_type(tt);
end

proc ti_infer_var_decl self, tt, st, ast -> type in
    check_ast_kind(AstK_VarDecl, ast);
    var ident = ast_ident(ast);
    var symbol = st_get_symbol(st, ident);
    if not symbol_type(symbol) then
        symbol_set_type(symbol, tt_any_type(tt));
    end
    return tt_void_type(tt);
end

proc _ti_infer_assignment tt, symbol, rhs_type, rhs_expr -> type in
    var sym_type = symbol_type(symbol);
    var any = tt_any_type(tt);
    if not sym_type or sym_type == any then
        symbol_set_type(symbol, rhs_type);
    elif type_is_reference(sym_type) and rhs_type == any then
        // nothing
    elif sym_type == tt_ptr_type(tt) and rhs_type != any then
        // this handles the case of `var x = NULL; x = make_vector(0);`
        symbol_set_type(symbol, rhs_type);
    end
end

proc ti_infer_var_assign self, tt, st, ast -> type in
    check_ast_kind(AstK_VarAssign, ast);
    var type = ti_infer(self, tt, st, ast_assign_expr(ast));
    var ident = ast_ident(ast);
    var symbol = st_get_symbol(st, ident);
    _ti_infer_assignment(tt, symbol, type, ast_assign_expr(ast));
    return tt_void_type(tt);
end

proc ti_infer_assign_op self, tt, st, ast -> type in
    check_ast_kind(AstK_AssignOp, ast);
    var op = ast_op(ast);
    var type = ti_infer(self, tt, st, ast_assign_expr(ast));

    if op != TK_Assign and not type_is_int_like(type) then
        printf("WARN: `");
        tt_print_type_name(tt, type);
        printf("` is not a primitive type and cannot be used with TODO: get operator string\n");
        printf(".`\nNOTE: The expression is: ");
        print_ast(ast);
        print_token_loc(ast_token(ast));
        //exit(1);
        return tt_void_type(tt);
    end

    var ident = ast_ident(ast);
    var symbol = st_get_symbol(st, ident);
    var sym_type = symbol_type(symbol);

    if op == TK_Assign then
        _ti_infer_assignment(tt, symbol, type, ast_assign_expr(ast));
    end
    return tt_void_type(tt);
end

proc ti_infer_procedure self, tt, st, ast -> type in
    check_ast_kind(AstK_Procedure, ast);
    var ident = ast_ident(ast);
    //printf("infer: %s\n", ident);
    var symbol = st_get_symbol(st, ident);
    ti_push_proc(self, symbol);
    ti_infer_body(self, tt, st, ast_body(ast));
    var proc_type = symbol_type(symbol);
    assert(type_is_procedure(proc_type),
           "Compiler bug: procedure type is not actually procedure type.\n");
    // don't handle the case where function is declared with a type but doesn't return here
    // because we'll let the IR compiler determine that.
    // it'just needless complexity to deal with checking every branch for returns here.
    if not ti_did_return(self) and type_proc_return_type(proc_type) == tt_any_type(tt) then
        type_set_proc_return_type(proc_type, tt_void_type(tt));
    end
    ti_pop_proc(self);
    return tt_void_type(tt);
end

proc ti_infer_if_else self, tt, st, ast -> type in
    check_ast_kind(AstK_IfElse, ast);
    // TODO: check test is bool compatible
    ti_infer(self, tt, st, ast_test(ast));
    ti_infer_body(self, tt, st, ast_consequence(ast));
    ti_infer_body(self, tt, st, ast_alternative(ast));
    return tt_void_type(tt);
end

proc ti_infer_while self, tt, st, ast -> type in
    check_ast_kind(AstK_While, ast);
    // TODO: check test is bool compatible
    ti_infer(self, tt, st, ast_test(ast));
    ti_infer_body(self, tt, st, ast_body(ast));
    return tt_void_type(tt);
end

proc ti_infer_pointer_read self, tt, st, ast -> type in
    check_ast_kind(AstK_PointerRead, ast);
    var args = ast_pop_args(ast);
    ti_infer(self, tt, st, vector_get(args, 0));
    var size = ast_size(ast);
    var type;
    if size == 64 then
        type = tt_get_type_or_null(tt, "int64");
    elif size == 32 then
        type = tt_get_type_or_null(tt, "int32");
    elif size == 16 then
        type = tt_get_type_or_null(tt, "int16");
    elif size == 8 then
        type = tt_get_type_or_null(tt, "int8");
    else
        assert(false, "Compiler bug: pointer read where size outside valid range; size was %d\n", size);
    end
    assert(type, "Compiler bug: didn't find correct type for u%d@\n", size);
    return type;
end

proc ti_infer_pointer_write self, tt, st, ast -> type in
    check_ast_kind(AstK_PointerWrite, ast);
    var args = ast_pop_args(ast);
    var i = 0; var len = args.length;
    while i < len do
        ti_infer(self, tt, st, vector_get(args, i));
        i += 1;
    end
    return tt_void_type(tt);
end

proc ti_infer_prefix_op self, tt, st, ast -> type in
    check_ast_kind(AstK_Prefix, ast);
    var t = ti_infer(self, tt, st, ast_expr(ast));
    if ast_op == TK_KW_not then
        return tt_bool_type(tt);
    end
    return t;
end

proc ti_infer_const self, tt, st, ast -> type in
    check_ast_kind(AstK_Const, ast);
    var type = ti_infer(self, tt, st, ast_assign_expr(ast));
    var ident = ast_ident(ast);
    var symbol = st_get_symbol(st, ident);
    _ti_infer_assignment(tt, symbol, type, ast_assign_expr(ast));
    return tt_void_type(tt);
end

proc ti_infer_extern self, tt, st, ast -> type in
    check_ast_kind(AstK_Extern, ast);
    var ident = ast_ident(ast);
    var symbol = st_get_symbol(st, ident);
    return tt_void_type(tt);
end

proc ti_infer_inline_asm self, tt, st, ast -> type in
    check_ast_kind(AstK_InlineAsm, ast);
    var ident = ast_ident(ast);
    var symbol = st_get_symbol(st, ident);
    return tt_void_type(tt);
end

proc ti_infer_global self, tt, st, ast -> type in
    check_ast_kind(AstK_Global, ast);
    var type = ti_infer(self, tt, st, ast_assign_expr(ast));
    var ident = ast_ident(ast);
    var symbol = st_get_symbol(st, ident);
    _ti_infer_assignment(tt, symbol, type, ast_assign_expr(ast));
    return tt_void_type(tt);
end

proc ti_infer_cast self, tt, st, ast -> type in
    check_ast_kind(AstK_Cast, ast);
    ti_infer(self, tt, st, ast_expr(ast));
    return ast_type(ast);
end

proc _ti_get_field_type type, member in
    var fields = type_struct_fields(type);
    var i = 0; var len = fields.length;
    while i < len do
        var field = vector_get(fields, i);
        if cstr_eq(field_name(field), member) then
            return field_type(field);
        end
        i += 1;
    end
    var procs = type_struct_procs(type);
    i = 0; len = procs.length;
    while i < len do
        var proc_ = vector_get(procs, i);
        if cstr_eq(symbol_name(proc_), member) then
            return symbol_type(proc_);
        end
        i += 1;
    end
    return NULL;
end

proc ti_infer_member_access self, tt, st, ast in
    check_ast_kind(AstK_MemberAccess, ast);
    var type = ti_infer(self, tt, st, ast_expr(ast));
    var member = ast_member(ast);
    var ftype = NULL;
    if type_is_struct(type) then
        ftype = _ti_get_field_type(type, member);
    elif type_is_reference(type) then
        var ref_type = type_deref_type(type);
        assert(ref_type, "reference type has no deref type");
        if not type_is_struct(ref_type) then
            printf("ERROR: type `");
            tt_print_type_name(tt, ref_type);
            printf("` is not a struct. did you forget a cast?\n");
            printf("NOTE: the expression was: ");
            print_ast(ast);
            print_ast_src_loc(ast);
            exit(1);
        end
        ftype = _ti_get_field_type(ref_type, member);
    elif type_is_enum(type) then
        return type;
    else
        ftype = tt_any_type(tt); // let it get resolved again later
    end
    if not ftype then
        printf("ERROR: type `");
        tt_print_type_name(tt, type);
        printf("` has no member `%s.`\n", ast_member(ast));
        print_ast_src_loc(ast);
        exit(1);
    end
    return ftype; 
end

proc ti_infer_assign_member self, tt, st, ast in
    // nothing really to infer, members are required type declarations.
    check_ast_kind(AstK_AssignMember, ast);
    ti_infer(self, tt, st, ast_lhs(ast));
    ti_infer(self, tt, st, ast_rhs(ast));
    return tt_void_type(tt);
end

proc ti_infer_sizeof_expr self, tt, st, ast in
    check_ast_kind(AstK_SizeofExpr, ast);
    var expr = ast_expr(ast);
    var type = ti_infer(self, tt, st, ast_expr(ast));
    if ast_kind(expr) == AstK_Ident then
        var sym = st_get_symbol(st, ast_ident(expr));
        type = symbol_type(sym);
        if type == tt_any_type(tt) then
            type = tt_get_type_or_null(tt, ast_ident(ast_expr(ast)));
        end
    end
    ast_change_sizeof_expr_to_type(ast, type);
    return tt_int_type(tt);
end

proc ti_infer self, tt, st, ast -> type in
    var k = ast_kind(ast);
    var type: type;
    if k == AstK_Ident then type = ti_infer_ident(self, tt, st, ast);
    elif k == AstK_Integer then type = ti_infer_integer(self, tt, st, ast);
    elif k == AstK_String then type = ti_infer_string(self, tt, st, ast);
    elif k == AstK_Call then type = ti_infer_call(self, tt, st, ast);
    elif k == AstK_BinOp then type = ti_infer_binop(self, tt, st, ast);
    elif k == AstK_Return then type = ti_infer_return(self, tt, st, ast);
    elif k == AstK_VarDecl then type = ti_infer_var_decl(self, tt, st, ast);
    elif k == AstK_VarAssign then type = ti_infer_var_assign(self, tt, st, ast);
    elif k == AstK_Procedure then type = ti_infer_procedure(self, tt, st, ast);
    elif k == AstK_IfElse then type = ti_infer_if_else(self, tt, st, ast);
    elif k == AstK_While then type = ti_infer_while(self, tt, st, ast);
    elif k == AstK_PointerRead then type = ti_infer_pointer_read(self, tt, st, ast);
    elif k == AstK_PointerWrite then type = ti_infer_pointer_write(self, tt, st, ast);
    elif k == AstK_Prefix then type = ti_infer_prefix_op(self, tt, st, ast);
    elif k == AstK_Const then type = ti_infer_const(self, tt, st, ast);
    elif k == AstK_Extern then type = ti_infer_extern(self, tt, st, ast);
    elif k == AstK_InlineAsm then type = ti_infer_inline_asm(self, tt, st, ast);
    elif k == AstK_Global then type = ti_infer_global(self, tt, st, ast);
    elif k == AstK_Break then type = tt_void_type(tt);
    elif k == AstK_Continue then type = tt_void_type(tt);
    elif k == AstK_AssignOp then type = ti_infer_assign_op(self, tt, st, ast);
    elif k == AstK_Cast then type = ti_infer_cast(self, tt, st, ast);
    elif k == AstK_Struct then type = tt_void_type(tt);
    elif k == AstK_MemberAccess then type = ti_infer_member_access(self, tt, st, ast);
    elif k == AstK_AssignMember then type = ti_infer_assign_member(self, tt, st, ast);
        // This seems like a bug but sizeof isn't supposed to evaluate its arguments.
    elif k == AstK_SizeofExpr then type = ti_infer_sizeof_expr(self, tt, st, ast);
    elif k == AstK_SizeofType then type = tt_int_type(tt);
    elif k == AstK_Enum then type = tt_void_type(tt);
    else
        printf("`ti_infer` in File \"%s\", line %d\n", __FILE__, __LINE__);
        printf("Unhandled AstK: %d\n", k);
        print_token(ast_token(ast));
        print_token_loc(ast_token(ast));
        print_ast(ast);
        exit(1);
    end
    tt_set_ast_type(tt, ast, type);
    return type;
end

proc type_infer type_table, symbol_table, compile_order: Vector& in
    var self = make_type_infer();
    var i = 0; var len = compile_order.length;
    while i < len do
        ti_infer(self, type_table, symbol_table, vector_get(compile_order, i));
        i += 1;
    end

    // TODO: prove this terminates or design an alternate solution.
    while true do
        var again = type_infer_again(self);
        if set_len(again) == 0 then
            break;
        end
        type_infer_set_again(self, make_set(0, NULL, NULL));
        var cur = set_begin(again);
        while cur != set_end(again) do
            var sym = cast(u64@(cur), symbol);
            var type = symbol_type(sym);
            var ast = type_ast(type);
            assert(ast, "ast from type_infer_again should not be null");
            ti_infer(self, type_table, symbol_table, ast);
            cur = set_next(again, cur);
        end
        free_set(again);
    end

    free_type_infer(self);
end
