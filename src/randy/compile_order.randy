
def _compile_order_visit tt, st, stmt, seen, order in
    if not stmt then
        return;
    end
    var k = ast_kind(stmt);
    if k == AstK::Call then
        _compile_order_visit_vector(tt, st, ast_args(stmt), seen, order);
        _compile_order_visit(tt, st, ast_expr(stmt), seen, order);
    elif k == AstK::Ident then
        var sym = st_get_symbol(st, ast_ident(stmt));
        if symbol_is_def(sym) then
            if seen.contains(sym) then
                return;
            end
            seen.insert(sym);
            var type = symbol_type(sym);
            assert(type, "Type for %s was null.\n", symbol_name(sym));
            var def_ = type_ast(type);
            assert(def_, "AST for symbol %s was null\n", symbol_name(sym));
            _compile_order_def(tt, st, def_, seen, order);
        end
    elif k == AstK::BinOp then
        _compile_order_visit(tt, st, ast_lhs(stmt), seen, order);
        _compile_order_visit(tt, st, ast_rhs(stmt), seen, order);
    elif k == AstK::Return then
        _compile_order_visit(tt, st, ast_expr(stmt), seen, order);
    elif k == AstK::VarAssign then
        _compile_order_visit(tt, st, ast_assign_expr(stmt), seen, order);
    elif k == AstK::IfElse then
        _compile_order_visit(tt, st, ast_test(stmt), seen, order);
        _compile_order_visit_vector(tt, st, ast_consequence(stmt), seen, order);
        _compile_order_visit_vector(tt, st, ast_alternative(stmt), seen, order);
    elif k == AstK::While then
        _compile_order_visit(tt, st, ast_test(stmt), seen, order);
        _compile_order_visit_vector(tt, st, ast_body(stmt), seen, order);
    elif k == AstK::PointerRead then
        _compile_order_visit(tt, st, ast_pop_args(stmt).get(0), seen, order);
    elif k == AstK::PointerWrite then
        _compile_order_visit(tt, st, ast_pop_args(stmt).get(0), seen, order);
        _compile_order_visit(tt, st, ast_pop_args(stmt).get(1), seen, order);
    elif k == AstK::Prefix then
        _compile_order_visit(tt, st, ast_expr(stmt), seen, order);
    elif k == AstK::AssignOp then
        _compile_order_visit(tt, st, ast_assign_expr(stmt), seen, order);
    elif k == AstK::Cast then
        _compile_order_visit(tt, st, ast_expr(stmt), seen, order);
    elif k == AstK::MemberAccess then
        _compile_order_visit(tt, st, ast_expr(stmt), seen, order);
    elif k == AstK::AssignMember then
        _compile_order_visit(tt, st, ast_lhs(stmt), seen, order);
        _compile_order_visit(tt, st, ast_rhs(stmt), seen, order);
    elif k == AstK::ScopeResolve then
        var type = scope_resolve_lhs(tt, ast_lhs(stmt), false);
        if type then
            _compile_order_visit_type(tt, st, type, seen, order);
        end
    // elif k == AstK::SizeofExpr then
    //     _compile_order_visit(tt, st, ast_expr(stmt), seen, order);
    end
end

def _compile_order_visit_type tt, st, type, seen, order in
    if seen.contains(type) then
        return;
    end
    seen.insert(type);
    if type_is_struct(type) then
        var defs = type_struct_defs(type);
        var i = 0; var len = defs.length;
        while i < len do
            var sym = defs.get(i);
            if not seen.contains(sym) then
                seen.insert(sym);
                assert(sym.type, "Type for %s was null.\n", sym.name);
                var def_ = type_ast(sym.type);
                assert(def_, "AST for symbol %s was null\n", sym.name);
                _compile_order_def(tt, st, def_, seen, order);
            end
            i += 1;
        end

        var fields = type_struct_fields(type);
        i = 0; len = fields.length;
        while i < len do
            var field = fields.get(i);
            _compile_order_visit_type(tt, st, field.type, seen, order);
            i += 1;
        end
    elif type_is_enum(type) then
        // nothing
    elif type_is_union(type) then
        var fields = type_union_fields(type);
        var i = 0; var len = fields.length;
        while i < len do
            var field = fields.get(i);
            _compile_order_visit_type(tt, st, field.type, seen, order);
            i += 1;
        end
    end
end

def _compile_order_visit_vector tt, st, vector, seen, order in
    var i = 0; var len = vector.length;
    while i < len do
        var stmt = vector.get(i);
        _compile_order_visit(tt, st, stmt, seen, order);
        i += 1;
    end
end

def _compile_order_def tt, st, def_, seen, order in
    _compile_order_visit_vector(tt, st, ast_body(def_), seen, order);
    order.push(def_);
end

def compile_order_main roots, type_table, symbol_table -> Vector[Ast&]& in
    var main_def = NULL;
    var exit_def = NULL;
    var compile_order = Vector[Ast&]::new(128);
    var i = 0; var len = roots.length;
    while i < len do
        var ast = roots.get(i);
        if ast_kind(ast) == AstK::Def then
            if cstr_eq("main", ast_ident(ast)) then
                main_def = ast;
            elif cstr_eq("exit", ast_ident(ast)) then
                exit_def = ast;
            end
        else
            compile_order.push(ast);
        end
        i += 1;
    end
    if not main_def then
        compile_order.delete();
        return NULL;
    end
    var seen = Set[ptr, PtrHashCompare, PtrHashCompare]::new(0);
    _compile_order_def(type_table, symbol_table, main_def, seen, compile_order);
    if exit_def and not seen.contains(st_get_symbol(symbol_table, ast_ident(exit_def))) then
        _compile_order_def(type_table, symbol_table, exit_def, seen, compile_order);
    end
    seen.delete();
    return compile_order;
end
