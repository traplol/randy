#include std/core
#include std/string
#include std/time

// This is an implementation of the Rule 110 cellular automaton
// more info at: https://en.wikipedia.org/wiki/Rule_110

def next_state state in
    var next = String::new(0);
    var left; var center; var right;
    var i = 0; var len = state.length;
    while i < len do
        if i == 0 then
            left = ' ';
        else
            left = state.get(i-1);
        end

        center = state.get(i);

        if i+1 == len then
            right = ' ';
        else
            right = state.get(i+1);
        end

          if left == '*' and center == '*' and right == '*' then next.push(' ');
        elif left == '*' and center == '*' and right == ' ' then next.push('*');
        elif left == '*' and center == ' ' and right == '*' then next.push('*');
        elif left == '*' and center == ' ' and right == ' ' then next.push(' ');
        elif left == ' ' and center == '*' and right == '*' then next.push('*');
        elif left == ' ' and center == '*' and right == ' ' then next.push('*');
        elif left == ' ' and center == ' ' and right == '*' then next.push('*');
        elif left == ' ' and center == ' ' and right == ' ' then next.push(' ');
        end
        i += 1;
    end
    return next;
end

def main in
    var steps = 500;
    var i = 0;
    var state = String::new_from_cstr("    *   *      *         *             *              *      *                 *");
    var next;
    while i < steps do
        printf("%s\n", state.cstr());
        next = next_state(state);
        state.delete();
        state = next;
        i += 1;
        //usleep(20 * CLOCK_MS_PER_US);
    end
    return 0;
end
