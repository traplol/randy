
#include string
#include core

const sizeof_TVECTOR_ELEMENT = sizeof_PTR;

struct Vector in
    buffer: ptr;
    length: int;
    capacity: int;
end

proc make_vector init: int -> Vector& in
    // var self = make_string(init * sizeof_TVECTOR_ELEMENT);
    // return cast(self, Vector&);
    var self = cast(malloc(__sizeof(Vector)), Vector&);
    self.buffer = malloc(sizeof_TVECTOR_ELEMENT * init);
    self.length = 0;
    self.capacity = init;
    return self;
end

proc free_vector self: Vector& in
    free_string(cast(self, String&));
end

proc vector_buf self: Vector& -> ptr in
    return self.buffer;
end

proc vector_len self: Vector& -> int in
    return self.length;
end

proc vector_cap self: Vector& -> int in
    return self.capacity;
end

proc vector_get self: Vector&, idx: int -> ptr in
    // TODO: Bounds check & abort
    return cast(u64@(self.buffer + (idx * sizeof_TVECTOR_ELEMENT)), ptr);
end

proc vector_set self: Vector&, idx: int, val: ptr in
    // TODO: Bounds check & abort
    u64!(self.buffer + (idx * sizeof_TVECTOR_ELEMENT), val);
end

proc vector_resize self: Vector&, new_cap: int in
    var new = malloc(new_cap * sizeof_TVECTOR_ELEMENT);
    var old = self.buffer;
    var len = self.length;
    if new_cap < len then
        len = new_cap;
    end
    memcpy(new, old, len * sizeof_TVECTOR_ELEMENT);
    free(old);
    self.buffer = new;
    self.length = len;
    self.capacity = new_cap;
end

proc vector_push self: Vector&, val: ptr in
    var len = self.length;
    var cap = self.capacity;
    if len >= cap then
        vector_resize(self, 2 * cap + 1);
    end
    u64!(self.buffer + len * sizeof_TVECTOR_ELEMENT, val);
    self.length = len + 1;
end

proc vector_pop self: Vector& in
    var len = self.length;
    if len > 0 then
        self.length = len - 1;
    else
        printf("ERROR: vector_pop out of bounds.\n");
        abort();
    end
end

proc vector_back self: Vector& -> ptr in
    var len = vector_len(self);
    if len > 0 then
        return cast(u64@(self.buffer + ((len - 1) * sizeof_TVECTOR_ELEMENT)), ptr);
    end
    printf("ERROR: vector_back out of bounds.\n");
    abort();
end

proc vector_set_len self: Vector&, len: int in
    self.length = len;
end
