
#include string
#include core

//const sizeof_TVECTOR_ELEMENT = sizeof_PTR;

struct Vector[T] in
    buffer: T&;
    length: int;
    capacity: int;

    def new init: int in
        static_assert(__sizeof(T) == 8, "vector of T where sizeof T is not 8 is not implemented.\n");
        var self = cast(malloc(__sizeof(Vector[T])), Vector[T]&);
        self.buffer = malloc(__sizeof(T) * init);
        self.length = 0;
        self.capacity = init;
        return self;
    end

    def delete self in
        if self then
            free(self.buffer);
            free(self);
        end
    end

    def get self, idx: int -> T in
        // TODO: Bounds check & abort
        return cast(u64@(self.buffer + (idx * __sizeof(T))), T);
    end

    def set self, idx: int, val: T in
        // TODO: Bounds check & abort
        u64!(self.buffer + (idx * __sizeof(T)), val);
    end

    def resize self, new_cap: int in
        var new = malloc(new_cap * __sizeof(T));
        var old = self.buffer;
        var len = self.length;
        if new_cap < len then
            len = new_cap;
        end
        memcpy(new, old, len * __sizeof(T));
        free(old);
        self.buffer = new;
        self.length = len;
        self.capacity = new_cap;
    end

    def push self, val: T in
        var len = self.length;
        var cap = self.capacity;
        if len >= cap then
            self.resize(2 * cap + 1);
        end
        u64!(self.buffer + len * __sizeof(T), val);
        self.length = len + 1;
    end

    def pop self in
        var len = self.length;
        if len > 0 then
            self.length = len - 1;
        else
            printf("ERROR: vector_pop out of bounds.\n");
            abort();
        end
    end

    def back self -> T in
        var len = self.length;
        if len > 0 then
            return cast(u64@(self.buffer + ((len - 1) * __sizeof(T))), T);
        end
        printf("ERROR: vector_back out of bounds.\n");
        abort();
    end

    def front self -> T in
        if self.length > 0 then
            return cast(u64@(self.buffer), T);
        end
        printf("ERROR: vector_front out of bounds.\n");
        abort();
    end

    def insert_front self, val: T in
        var len = self.length;
        var cap = self.capacity;
        if len >= cap then
            self.resize(2 * cap + 1);
        end

        if len == 0 then
            u64!(self.buffer, val);
            self.length = 1;
            return;
        end

        var buf = self.buffer;
        var tmp = u64@(buf + __sizeof(T));
        var x = u64@(buf);
        u64!(buf + __sizeof(T), x);
        while len > 1 do
            buf += __sizeof(T);
            x = tmp;
            tmp = u64@(buf + __sizeof(T));
            u64!(buf + __sizeof(T), x);
            len -= 1;
        end
        u64!(self.buffer, val);
        self.length += 1;
    end
end
