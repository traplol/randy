
#include core
#include memory

struct Queue[T] in
    buffer: T&;
    head: int;
    tail: int;
    capacity: int;

    def new init: int -> Queue[T]& in
        if init <= 0 then
            init = 16;
        end

        var self = cast(malloc(__sizeof(Queue[T])), Queue[T]&);
        var buf = malloc(init * __sizeof(T));
        self.buffer = buf;
        self.head = 0;
        self.tail = 0;
        self.capacity = init;
        return self;
    end

    def delete self in
        if self then
            free(self.buffer);
            free(self);
        end
    end

    def resize self, new_cap: int in
        var old_buf = self.buffer;
        var old_cap = self.capacity;
        if new_cap == old_cap then
            return;
        elif new_cap < old_cap then
            printf("queue:resize: shrinking queue unsupported.\n");
            abort();
        end
        var new_buf = malloc(new_cap * __sizeof(T));
        var head = self.head;
        var tail = self.tail;

        if tail - head > 0 then
            memcpy(new_buf, cast(old_buf + head, ptr), (tail - head) * __sizeof(T));
        end

        self.head = 0;
        self.tail = tail - head;
        self.buffer = new_buf;
        self.capacity = new_cap;
        free(old_buf);
    end

    def enqueue self, elem: T in
        var tail = self.tail;
        var cap = self.capacity;
        if tail == cap then
            self.resize(cap * 2 + 1);
        end

        var buf = self.buffer;
        u64!(buf + tail * __sizeof(T), elem);
        self.tail += 1;
    end

    def dequeue self -> T in
        var head = self.head;
        var tail = self.tail;
        if head == tail then
            printf("queue_dequeue: queue empty!\n");
            abort();
        end

        var val = cast(u@(self.buffer + head * __sizeof(T), T), T);
        if head + 1 == tail then
            self.head = 0;
            self.tail = 0;
        else
            self.head += 1;
        end
        return val;
    end

    def peek self -> T in
        var head = self.head;
        var tail = self.tail;
        if head == tail then
            printf("peek: queue empty!\n");
            abort();
        end

        return cast(u@(self.buffer + head * __sizeof(T), T), T);
    end

    def reset self in
        self.head = 0;
        self.tail = 0;
    end

    def length self -> int in
        return self.tail - self.head;
    end
end
